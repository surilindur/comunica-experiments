# https://www.wikidata.org/wiki/Wikidata:SPARQL_query_service/WDQS_graph_split/Federated_Queries_Examples#Number_of_articles_with_CiTO-annotated_citations_by_year
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdsubgraph: <https://query.wikidata.org/subgraph/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
select ?year (count(distinct(?work)) as ?number_of_publications) ?role where {
  ?cit_intent wdt:P31 wd:Q96471816 .
  {
    select ?work (min(?years) as ?year) ?type_ (coalesce(if(bound(?venue_), ?venue_, "__no_venue__")) AS ?venue_) where {
      {
        ?work wdt:P577 ?dates ;
              p:P2860 / pq:P3712 ?cit_intent .
        OPTIONAL {
          ?work wdt:P31 wd:Q109229154 .
          bind("explicit" as ?type_)
          ?work wdt:P1433 ?venue_ .
        }
      } UNION {
        SERVICE wdsubgraph:scholarly_articles {
          ?work wdt:P577 ?dates ;
                p:P2860 / pq:P3712 ?cit_intent .
          OPTIONAL {
            ?work wdt:P31 wd:Q109229154 .
            bind("explicit" as ?type_)
            ?work wdt:P1433 ?venue_ .
          }
        }
      }
      bind(str(year(?dates)) as ?years) .
    }
    group by ?work ?type_ ?venue_
  }
  OPTIONAL { ?venue_ rdfs:label ?venue . FILTER (LANG(?venue) = "en") }
  bind(
    coalesce(
      if(bound(?type_), ?venue,
      "other source")
    ) as ?role
  )
}
group by ?year ?role
order by ?year
