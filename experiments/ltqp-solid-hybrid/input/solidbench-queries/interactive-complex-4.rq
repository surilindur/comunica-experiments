# New topics
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www-ldbc-eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www-ldbc-eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www-ldbc-eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia-org/resource/>
PREFIX dbpedia-owl: <http://dbpedia-org/ontology/>

SELECT
    ?tagName
    (COUNT(*) AS ?postCount)
WHERE {
    BIND( ?startDate + STRDT(CONCAT("P", ?durationDays, "D"), xsd:duration) AS ?endDate ) .
    ?rootPerson a snvoc:Person .
    ?rootPerson ((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson)) ?fr .
    ?post a snvoc:Post .
    ?post snvoc:hasCreator ?fr .
    ?post snvoc:hasTag ?tag .
    ?tag foaf:name ?tagName .
    ?post snvoc:creationDate ?creationDate .
    FILTER (?creationDate >= ?startDate && ?creationDate <= ?endDate ) .
    FILTER NOT EXISTS {
        ?rootPerson ((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson)) ?fr2 .
        ?post2 snvoc:hasCreator ?fr2 .
        ?post2 a snvoc:Post .
        ?post2 snvoc:hasTag ?tag .
        ?post2 snvoc:creationDate ?creationDate2 .
        FILTER (?creationDate2 < ?startDate)
    }
}
GROUP BY ?tagName
ORDER BY DESC(?postCount) ?tagName
LIMIT 10
